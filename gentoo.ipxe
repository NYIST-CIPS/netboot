#!ipxe

# Gentoo Operating System
# http://www.gentoo.org

:gentoo
clear gentoo_version
set os Gentoo
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} i386 && set os_arch x86 ||
iseq ${os_arch} arm64 && set os_arch arm64 ||
menu ${os} - Current Arch [ ${os_arch} ]
item --gap ${os} Versions
iseq ${os_arch} amd64 && item 20240317T170433Z_amd64 ${space} ${os} 20240317T170433Z - amd64 ||
iseq ${os_arch} arm64 && item 20240428T234907Z_arm64 ${space} ${os} 20240428T234907Z - arm64 ||
iseq ${os_arch} x86 && item 20240129T170346Z_x86 ${space} ${os} 20240129T170346Z - x86 ||
choose gentoo_version || goto gentoo_exit
goto ${gentoo_version}

:20240317T170433Z_amd64
set url ${live_endpoint}/asset-mirror/releases/download/20240317T170433Z-63d6ec39/
goto boot
:20240428T234907Z_arm64
set url ${live_endpoint}/asset-mirror/releases/download/20240428T234907Z-d5972d15/
goto boot
:20240129T170346Z_x86
set url ${live_endpoint}/asset-mirror/releases/download/20240129T170346Z-5b773f72/
goto boot

:boot
imgfree
kernel ${url}vmlinuz ip=dhcp root=/dev/ram0 init=/linuxrc loop=/image.squashfs looptype=squashfs cdroot=1 initrd=initrd.magic ${cmdline}
initrd ${url}initrd
initrd ${url}image.squashfs /image.squashfs
boot

:gentoo_exit
clear menu
exit 0
